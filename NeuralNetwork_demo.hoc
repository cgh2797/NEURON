begintemplate SThcell
public soma, treeA, treeB ,nclist

create soma, treeA[1], treeB[1]
objectvar f, nclist

proc init() {local i, me, child1, child2 

    access soma

//set parameters for soma section
    nseg=1
    diam=18.8
    L = 18.8
    Ra=123.0

//add predefined channel parameters called
//hh - Hodgkin Huxley and pas - passive potassium and sodium
    insert hh
    gnabar_hh= 0.2
    gl_hh = .0001666
    el_hh = -60.0

    insert pas

    nclist = new List()


//load treeA
    f = new File()
    f.ropen("treeA.dat")
    ndendA = f.scanvar()
    create treeA[ndendA]

//define treeA

    for i = 0,ndendA-1 {
      me = f.scanvar() - 1
      child1 = f.scanvar() - 1
      child2 = f.scanvar() - 1

      treeA[me] {
        nseg = 1
        diam = f.scanvar()
        L = f.scanvar()
        Ra = 123

        insert pas
        g_pas = .0001666
        e_pas = -60.0

	// initialise and clear the 3D information
        pt3dclear()
        pt3dadd(f.scanvar(),f.scanvar(),f.scanvar(),diam)
        pt3dadd(f.scanvar(),f.scanvar(),f.scanvar(),diam)

        if (child1 >= 0) {
          connect treeA[child1](0), 1
        }
        if (child2 >= 0) {
          connect treeA[child2](0), 1
        }
      }
    }
f.close

//treeB

f = new File()
    f.ropen("treeB.dat")
    ndendB = f.scanvar()
    create treeB[ndendB]

    for i = 0,ndendB-1 {
      me = f.scanvar() - 1
      child1 = f.scanvar() - 1
      child2 = f.scanvar() - 1

      treeB[me] {
        nseg = 1
        diam = f.scanvar()
        L = f.scanvar()
        Ra = 123

        insert pas
        g_pas = .0001666
        e_pas = -60.0

	// initialise and clear the 3D information
        pt3dclear()
        pt3dadd(f.scanvar(),f.scanvar(),f.scanvar(),diam)
        pt3dadd(f.scanvar(),f.scanvar(),f.scanvar(),diam)

        if (child1 >= 0) {
          connect treeB[child1](0), 1
        }
        if (child2 >= 0) {
          connect treeB[child2](0), 1
        }
      }
    }
	f.close

    // Connect things to the soma
    connect treeA[0](0), soma(1)
    connect treeB[0](0), soma(0)
}
endtemplate SThcell







//create neuron from templete
nSThcells = 4
objectvar SThcells[nSThcells]

SThcells[0] = new SThcell()
SThcells[1] = new SThcell()
SThcells[2] = new SThcell()
SThcells[3] = new SThcell()

//graph default
access SThcells[0].soma



//insert current clamps

objectvar stim[nSThcells]


//SThcells[3] to activate SThcells[0]
i = 3
 SThcells[i].soma {
    stim[i] = new IClamp(0.5)
    stim[i].del = 100
    stim[i].dur = 100
    stim[i].amp = 0.9
}
maxsyn = 10
objectvar syn[maxsyn]


//connect SThcells[1] to the end of treeA dendritic branch 22 on SThcells[0]
access SThcells[0].treeA[22]
syn[0] = new ExpSyn(0)
access SThcells[1].soma
SThcells[0].nclist.append(new NetCon(&v(1), syn[0], 10, 1, 0.5))


//connect SThcells[2] to the end of treeA dendritic branch 22 on SThcells[1]
access SThcells[1].treeA[22]
syn[1] = new ExpSyn(0)
access SThcells[2].soma
SThcells[1].nclist.append(new NetCon(&v(1), syn[1], 10, 1, 0.5))

//connect SThcells[3] to the end of treeA dendritic branch 22 on SThcells[2]
access SThcells[2].treeA[22]
syn[2] = new ExpSyn(0)
access SThcells[3].soma
SThcells[2].nclist.append(new NetCon(&v(1), syn[2], 10, 1, 0.5))








print v
forall psection()
tstop=300
